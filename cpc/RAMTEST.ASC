10 MODE 2: INK 0,0 : BORDER 0: INK 1,26
20 PRINT "RAM Expansion Test, (C) 2018 Revaldinho"
30 PRINT
40 DEFINT a-z
50 DIM ifails(32)
60 DIM cfails(32)
65 DIM RAM(32)
70 ctot=0
80 mttot=0
90 FOR rwiter=0 TO 10
100   st!=TIME
110   adr=&4000+CINT(RND*16383)
115   GOSUB 1000 : REM Detect Ext. RAM Blocks
120   itertot=0
130   i=0
140   PRINT "Running block test and index write";
150   FOR bank=0 TO 7
160     FOR map=4 TO 7
165     IF RAM(i)=0 THEN GOTO 220
170       ifails(i)=0
180       OUT &7FFF,bank*8+map+&C0
190       GOSUB 570 : REM 6NTEST
200       mttot = mttot + mtfails
210       POKE adr,i
215       PRINT ".";
220       i=i+1
240     NEXT map
250   NEXT bank
260   PRINT
270   PRINT
280   OUT &7F00,&C0
290   POKE adr,&FF
300   PRINT "Read back index numbers"
310   i=0
320   FOR bank=0 TO 7
330     FOR map=4 TO 7
335     IF RAM(i)=0 THEN GOTO 420
340       OUT &7FFF,(bank*8)+map+&C0
350       r=PEEK(adr)
360       IF r=i THEN GOTO 420
370       ifails(i)=ifails(i)+1
380       itertot=itertot+ifails(i)
390       cfails(i)=cfails(i)+ifails(i)
400       PRINT i;":";ifails(i);"(";cfails(i);") ";
410       IF (i+1) MOD 4 =0 THEN PRINT
420       i=i+1
430     NEXT map
440   NEXT bank
450   ctot=ctot+itertot
460   PRINT "----------------------------------------------------"
470   PRINT "Iteration: ";rwiter;" Index Address:";HEX$(adr)
480   PRINT "Iteration Index Fails:";itertot
490   PRINT "Cumulative Index Fails:";ctot
500   PRINT "Cumulative memtest Fails:";mttot
510   PRINT "Iteration time:"; (TIME-st!)/300; "s"
520   PRINT "----------------------------------------------------"
530   IF ctot=0 THEN PRINT "** P A S S **" ELSE PRINT "** F A I L **"
540   PRINT "----------------------------------------------------"
550 NEXT rwiter
560 END
570 REM 6N Memory Test
580 mbot=&4000
590 mtop=&4007
600 mtfails=0
610 FOR a=mbot TO mtop:POKE a,&55:NEXT a
620 FOR a=mbot TO mtop
630   IF PEEK(a) <> &55 THEN GOSUB 740: REM Log fail
640   POKE a,&AA
650 NEXT a
660 FOR a=mtop TO mbot STEP -1
670   r=PEEK(a):POKE a,&55
680   IF r <> &AA THEN GOSUB 740
690 NEXT a
700 FOR a=mtop TO mbot STEP -1
710 IF PEEK(a)<>&55 THEN GOSUB 740
720 NEXT a
730 RETURN
740 REM Log fail
750 mtfails=mtfails+1
760 PRINT "Read fail at ";HEX$(a)
770 RETURN
1000 REM DETECT MEMORY SIZE
1005 i=0 : blocks=0
1010 FOR bank=0 TO 7
1020   FOR map=4 TO 7
1030     OUT &7FFF,(bank*8)+map+&C0
1040     POKE adr,i
1045     i=i+1
1050   NEXT map
1060 NEXT bank
1065 OUT &7FFF,&C0
1066 POKE adr, i
1067 i=0
1070 FOR bank=0 TO 7
1080   FOR map=4 TO 7
1081     RAM(i)=0
1090     OUT &7FFF,(bank*8)+map+&C0
1100     IF PEEK(adr)<>i THEN GOTO 1105
1101     blocks=blocks+1
1102     RAM(i)=1
1105     i=i+1
1110   NEXT map
1120 NEXT bank
1130 PRINT "Detected ";blocks;"blocks of Ext. RAM (";blocks*16;" KBytes)"
1140 RETURN
