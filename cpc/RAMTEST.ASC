10 MODE 2: INK 0,0 : BORDER 0: INK 1,26
20 PRINT "RAM Expansion Test, (C) 2018 Revaldinho"
30 PRINT
40 DEFINT a-z
50 DIM ifails(32)
60 DIM cfails(32)
70 DIM RAM(32)
80 ctot=0
90 mttot=0
100 FOR rwiter=0 TO 1
110   st!=TIME
120   adr=&4000+CINT(RND*16383)
130   GOSUB 830 : REM Detect Ext. RAM Blocks
140   itertot=0
150   i=0
160   PRINT "Running block test and index write";
170   FOR bank=0 TO 7
180     FOR map=4 TO 7
190     IF RAM(i)=0 THEN GOTO 260
200       ifails(i)=0
210       OUT &7FFF,bank*8+map+&C0
220       GOSUB 620 : REM 6NTEST
230       mttot = mttot + mtfails
240       POKE adr,i
250       PRINT ".";
260       i=i+1
270     NEXT map
280   NEXT bank
290   PRINT
300   PRINT
310   OUT &7F00,&C0
320   POKE adr,&FF
330   PRINT "Read back index numbers"
340   i=0
350   FOR bank=0 TO 7
360     FOR map=4 TO 7
370     IF RAM(i)=0 THEN GOTO 460
380       OUT &7FFF,(bank*8)+map+&C0
390       r=PEEK(adr)
400       IF r=i THEN GOTO 460
410       ifails(i)=ifails(i)+1
420       itertot=itertot+ifails(i)
430       cfails(i)=cfails(i)+ifails(i)
440       PRINT i;":";ifails(i);"(";cfails(i);") ";
450       IF (i+1) MOD 4 =0 THEN PRINT
460       i=i+1
470     NEXT map
480   NEXT bank
490   ctot=ctot+itertot
500   PRINT "----------------------------------------------------"
510   PRINT "Iteration: ";rwiter;" Index Address:";HEX$(adr)
520   PRINT "Iteration Index Fails:";itertot
530   PRINT "Cumulative Index Fails:";ctot
540   PRINT "Cumulative memtest Fails:";mttot
550   PRINT "Iteration time:"; (TIME-st!)/300; "s"
560   PRINT "----------------------------------------------------"
570   IF (mttot+ctot)=0 THEN PRINT "** P A S S **" ELSE PRINT "** F A I L **"
580   PRINT "----------------------------------------------------"
590 NEXT rwiter
600 GOSUB 1070
610 END
620 REM 6N Memory Test
630 mbot=&4000
640 mtop=&4003
650 mtfails=0
660 FOR a=mbot TO mtop:POKE a,&55:NEXT a
670 FOR a=mbot TO mtop
680   IF PEEK(a) <> &55 THEN GOSUB 790: REM Log fail
690   POKE a,&AA
700 NEXT a
710 FOR a=mtop TO mbot STEP -1
720   r=PEEK(a):POKE a,&55
730   IF r <> &AA THEN GOSUB 790
740 NEXT a
750 FOR a=mtop TO mbot STEP -1
760 IF PEEK(a)<>&55 THEN GOSUB 790
770 NEXT a
780 RETURN
790 REM Log fail
800 mtfails=mtfails+1
810 PRINT "Read fail at ";HEX$(a)
820 RETURN
830 REM DETECT MEMORY SIZE
840 i=0 : blocks=0
850 FOR bank=0 TO 7
860   FOR map=4 TO 7
870     OUT &7FFF,(bank*8)+map+&C0
880     POKE adr,i
890     i=i+1
900   NEXT map
910 NEXT bank
920 OUT &7FFF,&C0
930 POKE adr, i
940 i=0
950 FOR bank=0 TO 7
960   FOR map=4 TO 7
970     RAM(i)=0
980     OUT &7FFF,(bank*8)+map+&C0
990     IF PEEK(adr)<>i THEN GOTO 1020
1000     blocks=blocks+1
1010     RAM(i)=1
1020     i=i+1
1030   NEXT map
1040 NEXT bank
1050 PRINT "Detected ";blocks;"blocks of Ext. RAM (";blocks*16;" KBytes)"
1060 RETURN
1070 REM Summary
1080 i=0
1090 PRINT
1100 PRINT "S u m m a r y"
1110 PRINT
1115 GOSUB 830
1117 PRINT
1120 PRINT "Bank | Block Number"
1130 PRINT "No.  |   0    |    1   |    2   |    3   |"
1140 PRINT "-----+--------+--------+--------+--------+"
1145 i=0
1150 FOR bank=0 TO 7
1160   PRINT bank;"  ";
1170   FOR map=0 TO 3
1180     ON (RAM(i)+1) GOSUB 1250,1280
1190     i=i+1
1200     IF i MOD 4 = 0 THEN PRINT "|"
1210   NEXT map
1220 NEXT bank
1230 PRINT "-----+--------+--------+--------+--------+"
1240 RETURN
1250 REM RAM block absent
1260 PRINT "| ABSENT ";
1270 RETURN
1280 REM RAM block present
1290 IF cfails(i)=0 THEN PRINT "|  PASS  ";ELSE PRINT "|  FAIL  ";
1300 RETURN
