10 MODE 1: INK 0,1 : BORDER 1: INK 1,24
20 INK 2,18:INK 3,6:PAPER 0:PEN 1
30 PRINT "RAM Expansion Test, (C) 2018 Revaldinho"
40 PRINT
50 DEFINT a-z
60 DIM ifails(32)
70 DIM cfails(32)
80 DIM mtfails(32)
90 DIM RAM(32)
100 ctot=0
110 mttot=0
120 FOR i=0 TO 31
130   cfails(i)=0: mtfails(i)=0
140 NEXT i
150 FOR iter=0 TO 1
160   st!=TIME
170   adr=&4000+CINT(RND*16383)
180   GOSUB 920 : REM Detect Ext. RAM Blocks
190   PRINT
200   itertot=0
210   i=0
220   PRINT "Running block test and index write"
230   FOR bank=0 TO 7
240     FOR map=4 TO 7
250     IF RAM(i)=0 THEN GOTO 330
260       ifails(i)=0
270       OUT &7FFF,bank*8+map+&C0
280       GOSUB 710 : REM 6NTEST
290       mttot = mttot + mtf
300       mtfails(i)=mtfails(i) + mtf
310       POKE adr,i
320       PRINT ".";
330       i=i+1
340     NEXT map
350   NEXT bank
360   PRINT
370   PRINT
380   OUT &7F00,&C0
390   POKE adr,&FF
400   PRINT "Read back index numbers"
410   i=0
420   FOR bank=0 TO 7
430     FOR map=4 TO 7
440     IF RAM(i)=0 THEN GOTO 530
450       OUT &7FFF,(bank*8)+map+&C0
460       r=PEEK(adr)
470       IF r=i THEN GOTO 530
480       ifails(i)=ifails(i)+1
490       itertot=itertot+ifails(i)
500       cfails(i)=cfails(i)+ifails(i)
510       PRINT i;":";ifails(i);"(";cfails(i);") ";
520       IF (i+1) MOD 4 =0 THEN PRINT
530       i=i+1
540     NEXT map
550   NEXT bank
560   ctot=ctot+itertot
570   PRINT "---------------------------------------"
580   PRINT "Iteration               :";iter
590   PRINT "Iteration Index Fails   :";itertot
600   PRINT "Cumulative Index Fails  :";ctot
610   PRINT "Cumulative memtest Fails:";mttot
620   PRINT USING "Iteration time          :#####.## s"; (TIME-st!)/300
630   PRINT "---------------------------------------"
640   IF (mttot+ctot)=0 THEN PEN 2 ELSE PEN 3
650   IF (mttot+ctot)=0 THEN PRINT "** P A S S **" ELSE PRINT "** F A I L **"
660   PEN 1: PRINT "---------------------------------------"
680 NEXT iter
690 GOSUB 1170
700 END
710 REM 6N Memory Test
720 mbot=&4000
730 mtop=&4003
740 mtf=0
750 FOR a=mbot TO mtop:POKE a,&55:NEXT a
760 FOR a=mbot TO mtop
770   IF PEEK(a) <> &55 THEN GOSUB 880: REM Log fail
780   POKE a,&AA
790 NEXT a
800 FOR a=mtop TO mbot STEP -1
810   r=PEEK(a):POKE a,&55
820   IF r <> &AA THEN GOSUB 880
830 NEXT a
840 FOR a=mtop TO mbot STEP -1
850 IF PEEK(a)<>&55 THEN GOSUB 880
860 NEXT a
870 RETURN
880 REM Log fail
890 mtf=mtf+1
900 PRINT "Read fail at ";HEX$(a)
910 RETURN
920 REM DETECT MEMORY SIZE
930 i=0 : blocks=0
940 FOR bank=0 TO 7
950   FOR map=4 TO 7
960     OUT &7FFF,(bank*8)+map+&C0
970     POKE adr,i
980     i=i+1
990   NEXT map
1000 NEXT bank
1010 OUT &7FFF,&C0
1020 POKE adr, i
1030 i=0
1040 FOR bank=0 TO 7
1050   FOR map=4 TO 7
1060     RAM(i)=0
1070     OUT &7FFF,(bank*8)+map+&C0
1080     IF PEEK(adr)<>i THEN GOTO 1110
1090     blocks=blocks+1
1100     RAM(i)=1
1110     i=i+1
1120   NEXT map
1130 NEXT bank
1140 PRINT "Detected ";blocks;"blocks of Ext. RAM"
1150 PRINT "Total Memory = ";16*blocks+64" KBytes"
1160 RETURN
1170 REM Summary
1180 i=0
1190 PRINT
1200 PRINT "S u m m a r y"
1210 PRINT
1220 GOSUB 920
1230 PRINT
1240 PRINT "Bank | Block Number"
1250 PRINT "No.  |   0   |    1  |    2  |    3  |"
1260 PRINT "-----+-------+-------+-------+-------+"
1270 i=0
1280 FOR bank=0 TO 7
1290   PRINT bank;"  ";
1300   FOR map=0 TO 3
1310     ON (RAM(i)+1) GOSUB 1380,1410
1320     i=i+1
1330     IF i MOD 4 = 0 THEN PRINT "|"
1340   NEXT map
1350 NEXT bank
1360 PRINT "-----+-------+-------+-------+-------+"
1370 RETURN
1380 REM RAM block absent
1390 PRINT "| ABSENT";
1400 RETURN
1410 REM RAM block present
1420 IF (mtfails(i)+cfails(i))=0 THEN GOSUB 1450 ELSE GOSUB 1490
1430 RETURN
1440 REM
1450 PEN 1:PRINT"|";
1460 PEN 2:PRINT"  PASS ";
1470 PEN 1
1480 RETURN
1490 PEN 1:PRINT"|";
1500 PEN 3:PRINT" *FAIL*";
1510 PEN 1
1520 RETURN
